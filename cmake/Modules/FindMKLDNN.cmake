SET(MKLDNN_LIBRARIES)
SET(MKLDNN_INCLUDE_DIR)

SET(IDEEP_ROOT "${PROJECT_SOURCE_DIR}/third_party/ideep")
SET(MKLDNN_ROOT "${IDEEP_ROOT}/mkl-dnn")

FIND_PACKAGE(BLAS)
FIND_PATH(IDEEP_INCLUDE_DIR ideep.hpp PATHS ${IDEEP_ROOT} PATH_SUFFIXES include)
FIND_PATH(MKLDNN_INCLUDE_DIR mkldnn.hpp mkldnn.h PATHS ${MKLDNN_ROOT} PATH_SUFFIXES include)
IF (NOT MKLDNN_INCLUDE_DIR)
  EXECUTE_PROCESS(COMMAND git${CMAKE_EXECUTABLE_SUFFIX} submodule update --init mkl-dnn WORKING_DIRECTORY ${IDEEP_ROOT})
  FIND_PATH(MKLDNN_INCLUDE_DIR mkldnn.hpp mkldnn.h PATHS ${MKLDNN_ROOT} PATH_SUFFIXES include)
ENDIF(NOT MKLDNN_INCLUDE_DIR)

IF (NOT IDEEP_INCLUDE_DIR OR NOT MKLDNN_INCLUDE_DIR)
  MESSAGE(STATUS "MKLDNN source files not found!")
  RETURN()
ENDIF(NOT IDEEP_INCLUDE_DIR OR NOT MKLDNN_INCLUDE_DIR)
LIST(APPEND MKLDNN_INCLUDE_DIR ${IDEEP_INCLUDE_DIR})

# Configure MKL-DNN
ADD_DEFINITIONS(-DUSE_MKL)
SET(MKLDNN_USE_MKL "FULL")
INCLUDE_DIRECTORIES(AFTER ${MKL_INCLUDE_DIR})
LIST(APPEND mkldnn_LINKER_LIBS ${MKL_LIBRARIES})

LIST(APPEND MKLDNN_LIBRARIES ${MKL_LIBRARIES})
LIST(APPEND MKLDNN_INCLUDE_DIR ${MKL_INCLUDE_DIR})
MESSAGE(STATUS "MKL_LIBRARIES ${MKLDNN_LIBRARIES}")
MESSAGE(STATUS "MKL_INCLUDE_DIR ${MKLDNN_INCLUDE_DIR}")

# The OMP-related variables of MKL-DNN have to be overrode here
# if MKL is used. Then, the OMP version can be defined by MKL.
# MKL_LIBRARIES_xxxx_LIBRARY is defined by MKL.
IF (EXISTS "${MKL_LIBRARIES_gomp_LIBRARY}")
  SET(MKLIOMP5LIB ${MKL_LIBRARIES_gomp_LIBRARY} CACHE STRING "Override MKL-DNN omp dependency" FORCE)
ELSEIF(EXISTS "${MKL_LIBRARIES_iomp5_LIBRARY}")
  SET(MKLIOMP5LIB ${MKL_LIBRARIES_iomp5_LIBRARY} CACHE STRING "Override MKL-DNN omp dependency" FORCE)
ELSEIF(EXISTS "${MKL_LIBRARIES_libiomp5md_LIBRARY}")
  SET(MKLIOMP5DLL ${MKL_LIBRARIES_libiomp5md_LIBRARY} CACHE STRING "Override MKL-DNN omp dependency" FORCE)
ELSE(EXISTS "${MKL_LIBRARIES_gomp_LIBRARY}")
  SET(MKLIOMP5LIB "" CACHE STRING "Override MKL-DNN omp dependency" FORCE)
  SET(MKLIOMP5DLL "" CACHE STRING "Override MKL-DNN omp dependency" FORCE)
ENDIF(EXISTS "${MKL_LIBRARIES_gomp_LIBRARY}")

LIST(APPEND __mkldnn_looked_for MKLDNN_LIBRARIES)
LIST(APPEND __mkldnn_looked_for MKLDNN_INCLUDE_DIR)
INCLUDE(FindPackageHandleStandardArgs)
find_package_handle_standard_args(MKLDNN DEFAULT_MSG ${__mkldnn_looked_for})

SET(MKLDNN_THREADING "OMP:COMP")
ADD_SUBDIRECTORY(${MKLDNN_ROOT})
